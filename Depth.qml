import QtQuick
import Quickshell
import Quickshell.Io
import Quickshell.Wayland
import QtMultimedia

Scope {
    id: root
    Colors {
        id: colors
    }

    property int barCount: 60
    property int maxBarWidth: 300
    property int barHeight: 15
    property int barGap: 10
    property var cavaData: new Float32Array(barCount)

    // Process to run cava
    Process {
        id: cavaProcess
        running: true

        command: ["sh", "-c", "printf '[general]\\nbars=" + barCount + "\\nframerate=30\\n[output]\\nmethod=raw\\nraw_target=/dev/stdout\\ndata_format=ascii\\nascii_max_range=1000\\n[smoothing]\\nintegral=50\\ngravity=100\\n' | cava -p /dev/stdin"]
        stdout: SplitParser {
            splitMarker: "\n"
            onRead: data => {
                        const raw = data
                        if (!raw)
                        return

                        const bars = raw.split(";")
                        const len = bars.length > 0 && bars[bars.length - 1] === "" ? bars.length - 1 : bars.length

                        if (len >= root.barCount) {
                            const vals = new Float32Array(root.barCount)
                            for (var i = 0; i < root.barCount; i++) {
                                const n = +bars[i]
                                vals[i] = isNaN(n) ? 0 : n / 1000.0
                            }
                            root.cavaData = vals
                        }
                    }
        }
        stderr: SplitParser {
            onRead: data => console.log("Cava Debug:", data)
        }
    }
    Variants {
        model: Quickshell.screens
        PanelWindow {
            id: window
            required property var modelData

            screen: modelData
            WlrLayershell.layer: WlrLayer.Background
            WlrLayershell.namespace: "depth-wallpaper-below"

            anchors {
                left: true
                right: true
                top: true
                bottom: true
            }

            color: "black"

            SystemClock {
                id: clock
                precision: SystemClock.Minutes
            }
            // Layer 1: Video wallpaper
            Video {
                id: wallpaper
                source: Qt.resolvedUrl("assets/wallpapers/solo_30fps.mp4")

                anchors.fill: parent
                fillMode: VideoOutput.PreserveAspectCrop

                loops: MediaPlayer.Infinite
                volume: 0
                autoPlay: true
                z: 0
                enabled: false
            }
            // Layer 2: Right Text
            Text {
                id: timehour
                text: clock.date.toLocaleString(Qt.locale("en_US"), "h")
                font.pixelSize: 300
                font.bold: true
                font.family: "Glirock"
                color: colors.color9
                z: 1
                anchors {
                    horizontalCenter: parent.horizontalCenter
                    horizontalCenterOffset: -parent.width * 0.15
                    bottom: parent.bottom
                    bottomMargin: parent.height * 0.650
                }
            }

            // Layer 2: Right Text
            Text {
                id: timemin
                text: clock.date.toLocaleString(Qt.locale("en_US"), "mm")
                font.pixelSize: 300
                font.bold: true
                font.family: "Glirock"
                color: colors.color6
                z: 1
                anchors {
                    horizontalCenter: parent.horizontalCenter
                    horizontalCenterOffset: parent.width * 0.104
                    bottom: parent.bottom
                    bottomMargin: parent.height * 0.650
                }
            }
            // Layer 2.5: Cava visualizer (middle)
            Item {
                anchors.fill: parent
                z: 1.5
                enabled: false
                Row {
                    anchors {
                        horizontalCenter: parent.horizontalCenter
                        bottom: parent.bottom
                        bottomMargin: 550
                    }
                    spacing: root.barGap
                    Repeater {
                        model: root.barCount
                        Rectangle {
                            id: barItem
                            readonly property real magnitude: root.cavaData[index] || 0
                            width: root.barHeight
                            height: 6 + (magnitude * root.maxBarWidth)
                            radius: root.barHeight / 2
                            anchors.bottom: parent.bottom
                            color: colors.color4
                            gradient: Gradient {
                                orientation: Gradient.Vertical
                                GradientStop {
                                    position: 0.0
                                    color: "#ffffff"
                                }
                                GradientStop {
                                    position: 0.3
                                    color: colors.color4
                                }
                                GradientStop {
                                    position: 1.0
                                    color: colors.color5
                                }
                            }
                            border.color: Qt.rgba(1, 1, 1, 0.2)
                            border.width: 1
                            opacity: 0.4 + magnitude * 0.6
                        }
                    }
                }
            }

            // Layer 3: PNG overlay (front)
            Image {
                id: personaOverlay
                source: Qt.resolvedUrl("assets/wallpapers/foreground.png")

                anchors.fill: parent
                fillMode: Image.PreserveAspectCrop
                z: 2
                enabled: false
            }
        }
    }
}
